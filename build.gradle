plugins {
    id 'java'
    id 'jacoco'
    id 'java-library'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        resources {
            srcDirs "example-definitions", "config"
        }
    }
}

group="io.opentelemetry.contrib"

repositories {
    mavenCentral()
}

dependencies {
    api group: 'jakarta.el', name: 'jakarta.el-api', version: '5.0.1'
    api group: 'org.glassfish', name: 'jakarta.el', version: '4.0.2'
    api group: 'org.slf4j', name: 'slf4j-api', version: '2.0.6'
    api group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.27'
    api group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
    api group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.14.2'
    api group: 'io.opentelemetry.proto', name: 'opentelemetry-proto', version: '0.18.0-alpha'
    api group: 'io.opentelemetry', name: 'opentelemetry-sdk-trace', version: '1.17.0'
    api group: 'com.google.guava', name: 'guava', version: '33.5.0-jre'
    api group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    api group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    api group: 'commons-cli', name: 'commons-cli', version: '1.5.0'
    api group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '3.1.0'
    api group: 'com.github.f4b6a3', name: 'uuid-creator', version: '5.2.0'
    api group: 'io.grpc', name: 'grpc-protobuf', version: '1.53.0'
    api group: 'io.grpc', name: 'grpc-stub', version: '1.53.0'
    api group: 'io.grpc', name: 'grpc-netty', version: '1.53.0'
    api group: 'io.netty', name: 'netty-tcnative-boringssl-static', version: '2.0.66.Final'
    api group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.25.2'

    implementation group: 'org.testng', name: 'testng', version: '7.11.0'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
}

test {
    useTestNG()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    additionalSourceDirs.from = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(sourceSets.main.output)
    reports {
        html.required.set(true)
        xml.required.set(true)
        xml.outputLocation.set(file("${buildDir}/jacoco/test.xml"))
        csv.required.set(false)
    }
}

task fatJar(type: Jar) {
    archiveClassifier.set("all")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'io.opentelemetry.contrib.generator.telemetry.cli.CLIProcessor'
    }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jar {
    manifest {
        attributes 'Main-Class': 'io.opentelemetry.contrib.generator.telemetry.cli.CLIProcessor'
    }
    finalizedBy fatJar
}
